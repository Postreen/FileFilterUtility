plugins {
    id 'java'
    id 'application'
}

group = 'com.sazonov'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass.set('com.sazonov.utility.Main')
}


ext {
    lombokVersion = '1.18.38'
    junitVersion = '5.10.0'
    commonsCliVersion = '1.9.0'
    logbackVersion = '1.5.27'
    springContext = '7.0.3'
    mockitoVersion = '5.12.0'
}

dependencies {
    implementation "commons-cli:commons-cli:${commonsCliVersion}"
    implementation("org.springframework:spring-context:${springContext}")
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

test {
    useJUnitPlatform()
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Assembles a fat JAR including all dependencies.'

    archiveBaseName.set('FileFilterUtility')
    archiveVersion.set('1.0')

    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(sourceSets.main.output)

    dependsOn(configurations.runtimeClasspath)
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

